plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
}

java {
    // toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation(files(System.getProperty("user.home") + "/AppData/Roaming/Hytale/install/$patchline/package/game/$game_build/Server/HytaleServer.jar"))
}

def serverRunDir = file("$projectDir/run")

idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = "--allow-op --disable-sentry --assets=\"${System.getProperty('user.home')}/AppData/Roaming/Hytale/install/$patchline/package/game/$game_build/Assets.zip\" --mods=\"${file('src/main').absolutePath}\""
        workingDirectory = serverRunDir.absolutePath
    }
}

test {
    useJUnitPlatform()
}

processResources {
    var expandProps = [
            'name'          : project.name,
            'version'       : project.version,
            'group'         : project.group,
            'description'   : mod_description,
            'website'       : website,
            'server_version': server_version,
            'entry_point'   : entry_point
    ]

    filesMatching(['manifest.json']) {
        expand(expandProps)
    }
    inputs.properties(expandProps)
}

task runServer(type: JavaExec) {
    group = 'application'
    description = 'Run Hytale Server with mod'
    
    mainClass = 'com.hypixel.hytale.Main'
    classpath = files("${System.getProperty('user.home')}/AppData/Roaming/Hytale/install/$patchline/package/game/$game_build/Server/HytaleServer.jar")
    
    workingDir = file("$projectDir/run")
    
    args = [
        '--allow-op',
        '--disable-sentry',
        "--assets=${System.getProperty('user.home')}/AppData/Roaming/Hytale/install/$patchline/package/game/$game_build/Assets.zip",
        "--mods=${file('src/main').absolutePath}"
    ]
    
    doFirst {
        file("$projectDir/run").mkdirs()
    }
}